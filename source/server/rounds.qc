void() Spawn_Enemy = 
{
	if (roundtype == 1)
	{
		if (spawn_a_zombieA())
		{
			Current_Zombies = Current_Zombies + 1;
		}
	}
	else if (roundtype == 2)
	{
		if (spawn_a_dogA())
		{
			Current_Zombies = Current_Zombies + 1;
		}
	}
}

float() getZombieTotal = {
	if (roundtype == 1) {
		if (rounds < 20) {
			switch (player_count) {
				case 0: float cnt[20] = {6, 8, 13, 18, 24, 27, 28, 28, 29, 33, 34, 36, 39, 41, 44, 47, 50, 53, 56}; return cnt[rounds - 1];
				case 1: float cnt2[20] = {7, 9, 15, 21, 27, 31, 32, 33, 34, 42, 45, 49, 54, 59, 64, 70, 76, 82, 89}; return cnt2[rounds - 1];
				case 2: float cnt3[20] = {11, 14, 23, 32, 41, 47, 48, 50, 51, 62, 68, 74, 81, 89, 97, 105, 114, 123, 133}; return cnt3[rounds - 1];
				case 3: float cnt4[20] = {14, 18, 30, 42, 54, 62, 64, 66, 68, 83, 91, 99, 108, 118, 129, 140, 152, 164, 178}; return cnt4[rounds - 1];
				default: return 0;
			}
		} else {
			switch (player_count) {
				case 0: return 0.09 * rounds * rounds - 0.0029 * rounds + 23.958;
				case 1: return 0.1882 * rounds * rounds - 0.4313 * rounds + 29.212;
				case 2: return 0.2637 * rounds * rounds + 0.1802 * rounds + 35.015;
				case 3: return 0.35714 * rounds * rounds - 0.0714 * rounds + 50.4286;
				default: return 0;
			}
		}
	} else { //dogs
		if (dogWave == 1 || dogWave == 2)
			return 6 * (player_count + 1);

		return 8 * (player_count + 1);
	}
	return 0;
}

void(string s) playSoundAtPlayers =
{
	local entity p;
	p = find(world,classname,"player");
	while(p)
	{
		sound(p,CHAN_AUTO,s,1,ATTN_NONE);
		p = find(p,classname,"player");
	}
}

void() updateDogRound =
{
	float r = random();

	if (r < 0.33) {
		dogRound = rounds + 5;
	} else if (r < 0.66) {
		dogRound = rounds + 6;
	} else {
		dogRound = rounds + 7;
	}

	dogWave++;
}

void() EndRound =
{
	rounds_change = 4;
	SetUpdate(self, UT_ROUNDS_CHANGE, rounds_change, 0, 0);
	playSoundAtPlayers("sounds/rounds/eround.wav");
	round_changetime = time + 5;
}

void() PlayerSpawn;

void() NewRound =
{		
	entity who = find(world,classname,"spectator");
	while(who != world)
	{
		if(who.isspec)
		{
			self = who;
			PlayerSpawn();
			break;
		}
		
	}

	entity tempe;
	round_changetime = 0;
	spawn_time = time + 5;
	sounds_playing = 0;//just in case it ever somehow glitches
	if (delay_at_round > 0.08)
	{
		delay_at_round = delay_at_round*0.95;
		if (delay_at_round < 0.08)
			delay_at_round = 0.08;
	}
	if (rounds != 0)
	{
		playSoundAtPlayers("sounds/rounds/nround.wav");
		rounds_change = 6;
		SetUpdate(self, UT_ROUNDS_CHANGE, rounds_change, 0, 0);
		blink_return = time + 2;
	}

	// if we just had a dog round, set the next
	if (gotdog && rounds == dogRound) {
		updateDogRound();
	}

	rounds = rounds + 1;

	NotifyNewRound(rounds);
	tempe = find(world, classname, "player");
	while (tempe)
	{
		if(tempe.grenades & 1)
		{
			tempe.primary_grenades = tempe.primary_grenades + 2;
			if (tempe.primary_grenades > 4)
				tempe.primary_grenades = 4;
		}
		tempe = find(tempe, classname, "player");
	}

	set_z_health();

	//temp dog stuff
	/*if (gotdog) {
		switch(rounds) {
			case 5:
				Remaining_Zombies = Total_Zombies = 10; break;
			case 10:
			case 15:
				Remaining_Zombies = Total_Zombies = 15; break;
		}
	}*/
	
	maxreward = 50 * rounds;
	if (maxreward > 500)
			maxreward = 500;
	totalreward = 0;
	Current_Zombies = 0;
	spawn_delay = 0;
	totalpowerups = 0;
	powerup_activate = powerup_activate*1.14;
	
	if (rounds == dogRound && gotdog) {
		roundtype = 2;
		localcmd("fog 0.25\n"); 
	} else {
		roundtype = 1;
		
		if (world.fog) {
			#ifdef PSP
			localcmd(strcat("fog ", world.fog));
			#else 
			localcmd(strcat("fog ", world_fog));
			#endif
		}
	}

	Remaining_Zombies = Total_Zombies = getZombieTotal();
	#ifdef PSP
	//MotoLegacy -- achievements crash on psp..
	/*if (rounds == 5)
	{
		WriteByte (MSG_ALL, 43);
		WriteByte (MSG_ALL, 0);
	}
	if (rounds == 10)
	{
		WriteByte (MSG_ALL, 43);
		WriteByte (MSG_ALL, 1);
	}
	if (rounds == 15)
	{
		WriteByte (MSG_ALL, 43);
		WriteByte (MSG_ALL, 2);
	}*/
	#else
	switch(rounds) {
		case 5: GiveAchievement(0, pl1); break;
		case 10: GiveAchievement(1, pl1); break;
		case 15: GiveAchievement(2, pl1); break;
	}
	#endif
}
void() Round_Core =
{
	if (game_over)
		return;
	
	if (round_changetime <= (time + 2) && !rounds && round_changetime) {
		rounds_change = 2;
	}

	if (round_changetime <= (time + 2) && rounds && round_changetime) {
		rounds_change = 5;
	}

	if (blink_return && blink_return < time && rounds_change == 6)
	{
		blink_return = time + 3;
		rounds_change = 7;
	}
	if (blink_return && blink_return < time && rounds_change == 7)
	{
		blink_return = 0;
		rounds_change = 0;
	}
	if (round_changetime >= time)
		return;
	if (round_changetime)
	{
		roundtype = 1;
			
		NewRound();
		return;
	}
	if (Total_Zombies > Current_Zombies && spawn_time < time)
	{
		Spawn_Enemy();
	}
	if (Remaining_Zombies < 1 && !Delay_Time)
	{
		Delay_Time = time + 2;
		rounds_change = 3;
	}
	else if (Delay_Time && Delay_Time < time)
	{
		Delay_Time = 0;
		EndRound();
	}
	
	SetUpdate(self, UT_ROUNDS_CHANGE, rounds_change, 0, 0);
}

void() InitRounds =
{		
	roundtype = 1;
	delay_at_round = 2/0.95;
	totalpowerups = 0;
	powerup_activate = 2000/1.14;
	spawnAllZombEnts();
	round_changetime = time + 3.5;
	rounds_change = 1;
	roundinit = 1;
	SetUpdate(self, UT_ROUNDS_CHANGE, rounds_change, 0, 0);
}